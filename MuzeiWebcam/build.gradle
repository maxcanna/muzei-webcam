apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def versionMajor = 2
def versionMinor = 6
def versionPatch = 0
def versionBuild = 0

android {
    compileSdk 35
    namespace 'net.luxteam.muzeiwebcam'
    buildFeatures.buildConfig = true

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
    }
}

tasks.register('getVersionNumber') {
    doLast {
        file("./versionNumber").text = "v${versionMajor}.${versionMinor}.${versionPatch}"
    }
}

tasks.register('getReleaseNotes', Exec) {
    commandLine 'sh', '-c', "git log --reverse --no-merges --format='- %s' \$(git log --merges --format='%H' -n 2)..HEAD > releaseNotes"
}

dependencies {
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.2'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.firebase:firebase-analytics:22.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:19.4.3'
}
